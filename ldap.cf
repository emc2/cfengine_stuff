# Configuration variables
bundle agent ldap_config
{
  vars:
      # Base for the ldap database
      "ldap_base" string => "dc=metricspace,dc=net";
      # FQDN of the ldap server
      "ldap_server" string => "ldap.metricspace.net";
}

# Set up an LDAP client with GSSAPI authentication
bundle agent ldap_client
{
  vars:
      # ldap.conf variables
      "ldap_conf[BASE]" string => "$(ldap_config.ldap_base)";
      "ldap_conf[URI]" string => "$(ldap_config.ldap_server)";
      "ldap_conf[SSL]" string => "start_tls";
      "ldap_conf[TLS_CACERT]" string => "$(base_config.ca_cert_file)";
      "ldap_conf[TLS_CERT]" string => "$(base_config.cert_files_path)/$(sys.uqhost)-cert.pem";
      "ldap_conf[TLS_KEY]" string => "$(base_config.cert_files_path)/$(sys.uqhost)-key.pem";
      "ldap_conf[SASL_MECH]" string => "GSSAPI";
      "ldap_conf[SASL_SECPROPS]" string => "noplain,noanonymous";

    freebsd::
      # location of ldap.conf
      "ldap_conf_file" string => "/usr/local/etc/openldap/ldap.conf";
      # LDAP certificate file
      "ldap_cert_file" string => "$(sys.uqhost)-cert.pem";
      # LDAP key file
      "ldap_key_file" string => "$(sys.uqhost)-key.pem";

  files:
      # Create the ldap.conf file from config variables
      "$(ldap_conf_file)"
      create => "true",
      perms => m("644"),
      comment => "Create ldap.conf file",
      classes => if_repaired("create_ldap_conf");

      "$(ldap_conf_file)"
      edit_line => set_config_values("ldap_client.ldap_conf"),
      comment => "Create ldap.conf file",
      classes => if_repaired("edit_ldap_conf");

      # Copy this host's LDAP certificate file from the policy host
      "$(cert_files_path)/$(ldap_cert_file)"
      create => "true",
      perms => m("644"),
      copy_from => secure_cp("$(base_config.file_store)/$(ldap_cert_file)",
                             "$(sys.policy_hub)"),
      comment => "Copy host LDAP certificate file",
      classes => if_repaired("ldap_cert");

      # Copy this host's LDAP key file from the policy host
      "$(cert_files_path)/$(ldap_key_file)"
      create => "true",
      perms => m("644"),
      copy_from => secure_cp("$(base_config.file_store)/$(ldap_key_file)",
                             "$(sys.policy_hub)"),
      comment => "Copy host LDAP key file",
      classes => if_repaired("ldap_key");

  reports:
    create_ldap_conf::
      "Created LDAP client configuration variables";

    edit_ldap_conf::
      "Set LDAP client configuration variables";

    ldap_cert::
      "Copied LDAP SSL certificate file into place";

    ldap_key::
      "Copied LDAP SSL key file into place";
}
