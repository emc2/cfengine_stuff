# Manage CA certificate file
bundle agent ca_cert
{
  vars:
      # CA certificate file
      "ca_cert_file" string => "ca-cert.pem";
      "ca_cert_path" string => "$(base_config.cert_files_path)/$(ca_cert_file)";

      # CA revocation list file
      "ca_crl_file" string => "ca-crl.pem";
      "ca_crl_path" string => "$(base_config.crl_files_path)/$(ca_crl_file)";

      # CA keys Java keystore
      "ca_keystore_file" string => "cacerts.keystore";
      "ca_keystore_path" string => "$(base_config.cert_files_path)/$(ca_keystore_file)";
      # The keystore password is not important, as file permissions
      # protect the file.  It has to go in rc.conf anyway, so it
      # cannot be properly secured.
      "ca_keystore_password" string => "unimportant";

  files:
      # Ensure cert files directory exists
      "$(base_config.cert_files_path)/."
      create => "true",
      perms => m("755"),
      comment => "Create certificate files directory",
      classes => if_repaired("create_cert_dir");

      # Ensure cert files directory exists
      "$(base_config.key_files_path)/."
      create => "true",
      perms => m("755"),
      comment => "Create key files directory",
      classes => if_repaired("create_key_dir");

      # Ensure cert files directory exists
      "$(base_config.crl_files_path)/."
      create => "true",
      perms => m("755"),
      comment => "Create CRL files directory",
      classes => if_repaired("create_crl_dir");

      # Copy CA certificate into place
      "$(ca_cert_path)"
      copy_from => secure_cp("$(base_config.file_store)/CA/$(ca_cert_file)",
                             "$(sys.policy_hub)"),
      comment => "Copy CA certificate file into place",
      classes => if_repaired("ca_cert_copy");

      # Set permissions on CA certificate
      "$(ca_cert_path)"
      perms => m("644"),
      comment => "Copy CA certificate file into place",
      classes => if_repaired("ca_cert_set_perms");

      # Copy CA revocation list into place
      "$(ca_crl_path)"
      copy_from => secure_cp("$(base_config.file_store)/CA/$(ca_crl_file)",
                             "$(sys.policy_hub)"),
      comment => "Copy CA crlificate file into place",
      classes => if_repaired("ca_crl_copy");

      # Set permissions on CA revocation list
      "$(ca_crl_path)"
      perms => m("644"),
      comment => "Copy CA crlificate file into place",
      classes => if_repaired("ca_crl_set_perms");

    # Only do the keystore if it's necessary
    ca_keystore::
      # Copy CA keystore into place
      "$(ca_keystore_path)"
      copy_from => secure_cp("$(base_config.file_store)/CA/$(ca_keystore_file)",
                             "$(sys.policy_hub)"),
      comment => "Copy CA keystore file into place",
      classes => if_repaired("ca_keystore_copy");

      # Set permissions on CA keystore
      "$(ca_keystore_path)"
      perms => m("644"),
      comment => "Copy CA keystore into place",
      classes => if_repaired("ca_keystore_set_perms");

  reports:
    ca_cert_copy::
      "Copied CA certificate into place";

    ca_cert_set_perms::
      "Set permissions on CA certificate to 644";

    ca_crl_copy::
      "Copied CA revocation list into place";

    ca_crl_set_perms::
      "Set permissions on CA revocation list to 644";

    ca_keystore_copy::
      "Copied CA keystore into place";

    ca_keystore_set_perms::
      "Set permissions on CA keystore to 644";

    create_cert_dir::
      "Create SSL certificate files directory";

    create_crl_dir::
      "Create SSL CRL files directory";

    create_key_dir::
      "Create SSL key files directory";
}
