# Configuration data for the LDAP system.
bundle agent ldap_config
{
  vars:
      # Base for the LDAP database.
      "ldap_base" string => "dc=metricspace,dc=net";
      # FQDN of the LDAP server.
      "ldap_server" string => "ldap.metricspace.net";
      # URI for LDAP server
      "ldap_server_uri" string => "ldaps://$(ldap_config.ldap_server)";

    freebsd::
      "ldap_conf_dir" string => "/usr/local/etc/openldap";
}

# Set up an LDAP client with GSSAPI authentication.  This only sets up
# the ldap.conf file, which controls LDAP tools like ldapsearch,
# ldapwhoami, ldapadd/modify, and friends.  Most applications using
# LDAP will have their own configuration variables.
#
# This sets up client certificates and GSSAPI authentication by
# default for the LDAP tools.
#
# Note: this bundle assumes kerberos_client, as well as ca_cert.
bundle agent ldap_client
{
  vars:
      # LDAP certificate file
      "ldap_cert_file" string => "$(sys.uqhost)-ldap-cert.pem";
      # LDAP key file
      "ldap_key_file" string => "$(sys.uqhost)-ldap-key.pem";
      # Path to LDAP certificate file
      "ldap_cert_path" string => "$(base_config.cert_files_path)/$(ldap_cert_file)";
      # Path to LDAP key file
      "ldap_key_path" string => "$(base_config.key_files_path)/$(ldap_key_file)";
      # name of ldap.conf
      "ldap_conf_file" string => "ldap.conf";
      # location of ldap.conf
      "ldap_conf_path" string => "$(ldap_config.ldap_conf_dir)/$(ldap_conf_file)";

  files:
      # Ensure the LDAP configuration directory exists.
      "$(ldap_config.ldap_conf_dir)/."
      create => "true",
      perms => m("644"),
      comment => "Create ldap configuration directory",
      classes => if_repaired("create_ldap_conf_dir");

      # Create and/or edit the ldap.conf file
      "$(ldap_conf_path)"
      create => "true",
      perms => m("644"),
      edit_template => "$(sys.workdir)/inputs/promises/ldap.conf.template",
      comment => "Create ldap.conf file",
      classes => if_repaired("set_ldap_conf");

      # Copy this host's LDAP certificate file from the policy host
      "$(ldap_cert_path)"
      create => "true",
      perms => m("644"),
      copy_from => secure_cp("$(base_config.file_store)/$(sys.uqhost)/$(ldap_cert_file)",
                             "$(sys.policy_hub)"),
      comment => "Copy host LDAP certificate file",
      classes => if_repaired("ldap_cert");

      # Copy this host's LDAP key file from the policy host
      "$(ldap_key_path)"
      create => "true",
      perms => m("600"),
      copy_from => secure_cp("$(base_config.file_store)/$(sys.uqhost)/$(ldap_key_file)",
                             "$(sys.policy_hub)"),
      comment => "Copy host LDAP key file",
      classes => if_repaired("ldap_key");

  reports:
    create_ldap_conf_dir::
      "Created LDAP configuration directory";

    set_ldap_conf::
      "Set LDAP client configuration variables";

    ldap_cert::
      "Copied LDAP SSL certificate file into place";

    ldap_key::
      "Copied LDAP SSL key file into place";
}


bundle agent ldap_server
{
  vars:
    freebsd::
      "ldap_module_dir" string => "/usr/local/libexec/openldap";
      "ldap_run_dir" string => "/var/run/openldap";
      "ldap_db_dir" string => "/var/db/openldap-data";
      "slapd_user" string => "ldap";
      "slapd_group" string => "ldap";
      "sasl_config_path" string => "/usr/local/lib/sasl2/slapd.conf";
      "sasl_auth_mux_dir" string => "/var/run/saslauthd";
      "sasl_auth_mux_path" string => "$(sasl_auth_mux_dir)/mux";
      "sasl_config_vars" slist => { "pwcheck_method: saslauthd",
                                    "saslauthd_path: $(sasl_auth_mux_path)" };

      "rc_conf[saslauthd_enable]" string => "YES";
      "rc_conf[saslauthd_flags]" string => "\"-a kerberos5\"";
      "rc_conf[slapd_enable]" string => "YES";
      "rc_conf[slapd_flags]" string => "\"-h 'ldapi:/// ldap:/// ldaps:///'\"";
      "rc_conf[slapd_sockets]" string => "/var/run/openldap/ldapi";
      "rc_conf[slapd_krb5_ktname]" string => "/etc/keytabs/ldap.keytab";

    any::
      # Directory for ldap schemas
      "slapd_schema_dir" string => "$(ldap_config.ldap_conf_dir)/schema";

      # Name of slapd.conf file
      "slapd_conf_file" string => "slapd.conf";
      # Path to slapd.conf file
      "slapd_conf_path" string => "$(ldap_config.ldap_conf_dir)/$(slapd_conf_file)";

      # Name of slapd access control file
      "slapd_access_file" string => "slapd.access";
      # Path to slapd access control file
      "slapd_access_path" string => "$(ldap_config.ldap_conf_dir)/$(slapd_access_file)";

      # slapd certificate file
      "slapd_cert_file" string => "$(sys.uqhost)-slapd-cert.pem";
      # slapd key file
      "slapd_key_file" string => "$(sys.uqhost)-slapd-key.pem";
      # Path to slapd certificate file
      "slapd_cert_path" string => "$(base_config.cert_files_path)/$(slapd_cert_file)";
      # Path to slapd file
      "slapd_key_path" string => "$(base_config.key_files_path)/$(slapd_key_file)";
      # NSS kerberos credential
      "slapd_cred" string => "ldap/$(sys.fqhost)";
      # slapd keytab file
      "slapd_keytab_file" string => "ldap.keytab";
      # Path to slapd keytab
      "slapd_keytab_path" string => "$(kerberos_config.keytabs_dir)/$(slapd_keytab_file)";

      "db_config_file" string => "DB_CONFIG";
      "db_config_path" string => "$(ldap_db_dir)/$(db_config_file)";

  files:
      # Ensure the LDAP configuration directory exists.
      "$(ldap_config.ldap_conf_dir)/."
      create => "true",
      perms => m("644"),
      comment => "Create ldap configuration directory",
      classes => if_repaired("create_ldap_conf_dir");

      # Copy slapd certificate file from the policy host
      "$(slapd_cert_path)"
      create => "true",
      perms => mog("644", "$(slapd_user)", "$(slapd_group)"),
      copy_from => secure_cp("$(base_config.file_store)/$(sys.uqhost)/$(slapd_cert_file)",
                             "$(sys.policy_hub)"),
      comment => "Copy slapd certificate file",
      classes => if_repaired("slapd_cert");

      # Copy slapd key file from the policy host
      "$(slapd_key_path)"
      create => "true",
      perms => mog("600", "$(slapd_user)", "$(slapd_group)"),
      copy_from => secure_cp("$(base_config.file_store)/$(sys.uqhost)/$(slapd_key_file)",
                             "$(sys.policy_hub)"),
      comment => "Copy slapd key file",
      classes => if_repaired("slapd_key");

      # Make sure the keytab has the right permissions
      "$(slapd_keytab_path)"
      create => "false",
      perms => mog("600", "$(slapd_user)", "$(slapd_group)"),
      comment => "Set permission of slapd keytab",
      classes => if_repaired("slapd_keytab_wrong_perms");

      # Create and/or edit the ldap.conf file
      "$(slapd_conf_path)"
      create => "true",
      perms => m("644"),
      edit_template => "$(sys.workdir)/inputs/promises/slapd.conf.template",
      comment => "Create slapd.conf file",
      classes => if_repaired("set_slapd_conf");

      # Create and/or edit the ldap.conf file
      "$(slapd_access_path)"
      create => "true",
      perms => m("644"),
      edit_template => "$(sys.workdir)/inputs/promises/slapd.access.template",
      comment => "Create slapd.access file",
      classes => if_repaired("set_slapd_access");

      # Create DB_CONFIG file
      "$(db_config_path)"
      create => "true",
      perms => mog("644", "$(slapd_user)", "$(slapd_group)"),
      edit_line => append_if_no_line("set_cachesize 0 67108864 0"),
      comment => "Create DB_CONFIG file in database directory",
      classes => if_repaired("create_db_config");

      # Setup sasl configuration file
      "$(sasl_config_path)"
      create => "true",
      perms => m("644"),
      edit_line => append_if_no_lines(@(sasl_config_vars)),
      comment => "Create sasl configuration file",
      classes => if_repaired("create_sasl_conf");

    freebsd::
      # Make sure the nslcd daemon is turned on
      "/etc/rc.conf"
      create => "true",
      perms => mog("644", "root", "wheel"),
      edit_line => set_variable_values("ldap_server.rc_conf"),
      comment => "Enable saslauthd and slapd in rc.conf",
      classes => if_repaired("enable_slapd_rc_conf");

  reports:
    create_sasl_conf::
      "Edit sasl configuration in $(sasl_config_path)";

    create_db_config::
      "Edited $(db_config_path) file";

    create_ldap_conf_dir::
      "Created LDAP configuration directory";

    create_ldap_schema_dir::
      "Created LDAP schema directory";

    slapd_key::
      "Copied slapd SSL key";

    slapd_cert::
      "Copied slapd SSL certificate";

    slapd_keytab_wrong_perms::
      "Set permissions on $(slapd_keytab_path).  WARNING: Keytab had wrong permissions; possible data breach!";

    set_slapd_conf::
      "Set LDAP server configuration variables";

    set_slapd_access::
      "Set LDAP server access policy";

    enable_slapd_rc_conf::
      "Enabled slapd and saslauthd in rc.conf";
}
