# This is the central point for managing configuration promises and
# selecting which ones apply to a given server.
bundle common host_config
{
  vars:
      # Clients for globally-controlled LDAP+Kerberos authentication.
      # These clients get Kerberized/LDAPized NSS and PAM.  Note: a
      # lot of the rest of this configuration assumes these bundles
      # are run.
      "global_auth_clients" slist => { "www",
                                       "mail",
                                       "xmpp",
                                       "proton",
                                       "tachyon"
                                     };

  classes:
      # Set this machine up as an ssh server (meaning it runs sshd).
      #
      # Note: on FreeBSD, the default key size needs to get turned up
      "ssh_server" or => { "tachyon",
                           "proton"
                         };

      # Set this machine up as a kerberos server machine.
      "kerberos_server" expression => "kdc";
      # Set this machine up as an LDAP server machine.
      "ldap_server" expression => "ldap";

      # Set these machines up as users of kerberos authentication.
      "kerberos_client" or => { "kdc",
                                "ldap",
                                "higgs",
                                @(global_auth_clients) };

      # Set these machines up as LDAP clients.
      "ldap_client" or => { "ldap",
                            @(global_auth_clients) };

      # Set up an LDAP server
      "ldap_server" expression => "ldap";

      # Set up LDAP-based NSS on these machines.
      "nslcd" or => { @(global_auth_clients) };

      # Set up kerberos-based saslauthd.
      "sasl" or => { @(global_auth_clients) };

      # Set up kerberos-based PAM authentication.
      "pam" or => { @(global_auth_clients) };

      # Set up a PostgreSQL client installation.
      "postgresql_client" or => { "www",
                                  "higgs",
                                  "xmpp"
                                };

      # Set up the PostgreSQL server.
      "postgresql_server" expression => "higgs";

      # Set up a mail server running dovecot and postfix.
      "mail_server" expression => "mail";
}

bundle common master_config 
{
  vars:
      # EVERYBODY gets their ssh client updated...
      "ssh_client_bundles" slist => { "ssh_client" };

    # These are lists of bundles to be run for each option.  Most of
    # the time an option isn't chosen, nothing is done.
    ssh_server::
      "ssh_server_bundles" slist => { "ssh_server" };

    !ssh_server::
      "ssh_server_bundles" slist => { "no_ssh_server" };

    kerberos_client::
      "kerberos_client_bundles" slist => { "kerberos_client" };

    !kerberos_client::
      "kerberos_client_bundles" slist => {};

    kerberos_server::
      "kerberos_server_bundles" slist => { "kerberos_server" };

    !kerberos_server::
      "kerberos_server_bundles" slist => {};

    ldap_client::
      "ldap_client_bundles" slist => { "ldap_client" };

    !ldap_client::
      "ldap_client_bundles" slist => {};

    ldap_server::
      "ldap_server_bundles" slist => { "ldap_server" };

    !ldap_server::
      "ldap_server_bundles" slist => {};

    nslcd::
      "nslcd_bundles" slist => { "nslcd" };

    !nslcd::
      "nslcd_bundles" slist => {};

    # Nothing to do for postgresql clients for now...
    postgresql_client::
      "postgresql_client_bundles" slist => {};

    !postgresql_client::
      "postgresql_client_bundles" slist => {};

    postgresql_server::
      "postgresql_server_bundles" slist => { "postgresql_server" };

    !postgresql_server::
      "postgresql_server_bundles" slist => {};

    mail_server::
      "mail_server_bundles" slist => { "dovecot" };

    !mail_server::
      "mail_server_bundles" slist => {};

    freebsd::
      "os_bundles" slist => { "src_conf_disable_junk" };

    any::
      # List of all input files for promise bundles we use.
      "inputs" slist => { "promises/base_config.cf",
                          "promises/ca_cert.cf",
                          "promises/src_conf.cf",
                          "promises/ssh.cf",
                          "promises/nslcd.cf",
                          "promises/kerberos.cf",
                          "promises/ldap.cf",
                          "promises/postgresql.cf",
                          "promises/mail.cf",
                          "promises/dovecot.cf"
                        };

      # This is the master list of bundles that will be run.  We
      # always install the CA certificate.  Beyond that, we run any
      # OS-specific bundles, then start running the service specific
      # bundle lists.
      "bundles" slist => { "ca_cert",
                           @(os_bundles),
                           @(ssh_client_bundles),
                           @(ssh_server_bundles),
                           @(kerberos_client_bundles),
                           @(kerberos_server_bundles),
                           @(ldap_client_bundles),
                           @(ldap_server_bundles),
                           @(nslcd_bundles),
                           @(postgresql_client_bundles),
                           @(postgresql_server_bundles),
                           @(mail_server_bundles)
                         };
}
