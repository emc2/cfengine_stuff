# Basic configuration for pluggable authentication modules.  This sets
# up kerberos authentication using PAM, thereby allowing su, console
# logins, and other such tools to use kerberos.
bundle agent pam {
  vars:
    freebsd::
      # The directory containing pam files
      "pam_dir" string => "/etc/pam.d";
      # The files that need updating
      "pam_files" slist => { "system", "imap", "pop3" };
      "template_file" string => "pam.template.freebsd";

    darwin::
      "pam_dir" string => "/etc/pam.d";
      "pam_files" slist => { "authorization" };
      "template_file" string => "pam.template.darwin";

    any::
      "filepath_$(pam_files)" string => "$(pam_dir)/$(pam_files)";

  classes:
    freebsd::
      "ports_pam_krb5" expression =>
      fileexists("/usr/local/lib/security/pam_krb5.so");

  files:
      # Edit specific pam configuration files
      "$(filepath_$(pam_files))"
      create => "true",
      edit_template => "$(sys.workdir)/inputs/promises/$(template_file)",
      comment => "Set pam configuration",
      classes => if_repaired("edit_pam_conf_$(pam_files)");

      # Set permission of pam configuration files
      "$(filepath_$(pam_files))"
      create => "false",
      perms => m("644"),
      comment => "Set pam configuration",
      classes => if_repaired("pam_conf_perms_$(pam_files)");

  reports:
      "Edited pam configuration in $(filepath_$(pam_files))"
      ifvarclass => "edit_pam_conf_$(pam_files)";

      "Set permissions of $(filepath_$(pam_files)) to 644"
      ifvarclass => "pam_conf_perms_$(pam_files)";
}
