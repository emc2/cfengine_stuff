bundle agent tomcat {

  vars:
    freebsd::
      "base_dir" string => "/usr/local/apache-tomcat-7.0";

    any::
      # Directory for config files
      "conf_dir" string => "$(base_dir)/conf";

      # JAAS login.conf
      "login_conf_file" string => "login.conf";
      "login_conf_path" string => "$(conf_dir)/$(login_conf_file)";

      # Tomcat kerberos credential.  This is used both for
      # authentication with clients, as well as for authentication
      # with the LDAP database.
      "cred" string => "tomcat/$(sys.fqhost)";
      # NSS keytab file
      "keytab_file" string => "tomcat.keytab";
      # Path to tomcat keytab
      "keytab_path" string => "$(kerberos_config.keytabs_dir)/$(keytab_file)";

      # The client certificate used by postfix to talk to clients
      "ldap_cert_file" string => "$(sys.uqhost)-tomcat-ldap-cert.pem";
      "ldap_cert_path" string => "$(base_config.cert_files_path)/$(ldap_cert_file)";

      # The client key used by postfix to talk to clients
      "ldap_key_file" string => "$(sys.uqhost)-tomcat-ldap-key.pem";
      "ldap_key_path" string => "$(base_config.key_files_path)/$(ldap_key_file)";

      # The keystore file
      "keystore_file" string => "tomcat.keystore";
      "keystore_path" string => "$(base_config.key_files_path)/$(keystore_file)";

      "user" string => "www";
      "group" string => "www";

  files:
      # Copy the server certificate file from the policy host
      "$(ldap_cert_path)"
      create => "true",
      perms => mog("644", "$(user)", "$(group)"),
      copy_from => secure_cp("$(base_config.file_store)/$(sys.uqhost)/$(ldap_cert_file)",
                             "$(sys.policy_hub)"),
      comment => "Copy tomcat-to-ldap certificate file",
      classes => if_repaired("ldap_cert");

      # Copy the server key file from the policy host
      "$(ldap_key_path)"
      create => "true",
      perms => mog("600", "$(user)", "$(group)"),
      copy_from => secure_cp("$(base_config.file_store)/$(sys.uqhost)/$(ldap_key_file)",
                             "$(sys.policy_hub)"),
      comment => "Copy tomcat-to-ldap key file",
      classes => if_repaired("ldap_key");

      # Copy the openfire keystore
      "$(keystore_path)"
      create => "true",
      perms => mog("600", "$(user)", "$(group)"),
      copy_from => secure_cp("$(base_config.file_store)/$(sys.uqhost)/$(keystore_file)",
                             "$(sys.policy_hub)"),
      comment => "Copy tomcat keystore file",
      classes => if_repaired("copy_keystore");

      # Make sure the keytab has the right permissions
      "$(keytab_path)"
      create => "false",
      perms => mog("600", "$(user)", "$(group)"),
      comment => "Set permission of postfix keytab",
      classes => if_repaired("postfix_keytab_wrong_perms");

  reports:
    ldap_key::
      "Copied tomcat-to-ldap SSL key";

    ldap_cert::
      "Copied tomcat-to-ldap SSL certificate";

    copy_keystore::
      "Copied tomcat keystore"
}
