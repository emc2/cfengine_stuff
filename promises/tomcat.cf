bundle agent tomcat {

  vars:
    freebsd::
      "base_dir" string => "/usr/local/apache-tomcat-7.0";

      # User and group
      "user" string => "www";
      "group" string => "www";

      # Variables to get set in rc.conf
      "rc_conf[tomcat7_enable]" string => "YES";
      "tomcat7_java_opts" string => "tomcat7_java_opts=\"-Djava.security.auth.login.config=$(login_conf_path) -Djavax.net.ssl.keyStore=$(keystore_path) -Djavax.net.ssl.keyStorePassword=$(keystore_password) -Djavax.net.ssl.trustStore=$(ca_cert.ca_keystore_path) -Djavax.net.ssl.trustStorePassword=$(ca_cert.ca_keystore_password) -server -Xmx1536m -Xms1536m -XX:NewSize=256m -XX:MaxNewSize=256m -XX:PermSize=256m -XX:MaxPermSize=256m -XX:+DisableExplicitGC\"";

    any::
      # Directory for config files
      "conf_dir" string => "$(base_dir)/conf";

      # JAAS login.conf
      "login_conf_file" string => "login.conf";
      "login_conf_path" string => "$(conf_dir)/$(login_conf_file)";

      # Tomcat kerberos credential.  This is used both for
      # authentication with clients, as well as for authentication
      # with the LDAP database.
      "cred" string => "tomcat/$(sys.fqhost)";
      # NSS keytab file
      "keytab_file" string => "tomcat.keytab";
      # Path to tomcat keytab
      "keytab_path" string => "$(kerberos_config.keytabs_dir)/$(keytab_file)";

      # The client certificate used by postfix to talk to clients
      "ldap_cert_file" string => "$(sys.uqhost)-tomcat-ldap-cert.pem";
      "ldap_cert_path" string => "$(base_config.cert_files_path)/$(ldap_cert_file)";

      # The client key used by postfix to talk to clients
      "ldap_key_file" string => "$(sys.uqhost)-tomcat-ldap-key.pem";
      "ldap_key_path" string => "$(base_config.key_files_path)/$(ldap_key_file)";

      # The keystore file
      "keystore_file" string => "tomcat.keystore";
      "keystore_path" string => "$(base_config.key_files_path)/$(keystore_file)";
      # The keystore password is not important, as file permissions
      # protect the file.  It has to go in rc.conf anyway, so it
      # cannot be properly secured.
      "keystore_password" string => "changeit";

      # The login.conf file for tomcat
      "login_conf_file" string => "login.conf";
      "login_conf_path" string => "$(tomcat.conf_dir)/$(login_conf_file)";

      # The tomcat server.xml file
      "server_xml_file" string => "server.xml";
      "server_xml_path" string => "$(tomcat.conf_dir)/$(server_xml_file)";

      # The tomcat server.xml file
      "context_xml_file" string => "context.xml";
      "context_xml_path" string => "$(tomcat.conf_dir)/$(context_xml_file)";

  files:
      # Copy the server certificate file from the policy host
      "$(ldap_cert_path)"
      create => "true",
      perms => mog("644", "$(user)", "$(group)"),
      copy_from => secure_cp("$(base_config.file_store)/$(sys.uqhost)/$(ldap_cert_file)",
                             "$(sys.policy_hub)"),
      comment => "Copy tomcat-to-ldap certificate file",
      classes => if_repaired("ldap_cert");

      # Copy the server key file from the policy host
      "$(ldap_key_path)"
      create => "true",
      perms => mog("600", "$(user)", "$(group)"),
      copy_from => secure_cp("$(base_config.file_store)/$(sys.uqhost)/$(ldap_key_file)",
                             "$(sys.policy_hub)"),
      comment => "Copy tomcat-to-ldap key file",
      classes => if_repaired("ldap_key");

      # Copy the openfire keystore
      "$(keystore_path)"
      create => "true",
      perms => mog("600", "$(user)", "$(group)"),
      copy_from => secure_cp("$(base_config.file_store)/$(sys.uqhost)/$(keystore_file)",
                             "$(sys.policy_hub)"),
      comment => "Copy tomcat keystore file",
      classes => if_repaired("copy_keystore");

      # Make sure the keytab has the right permissions
      "$(keytab_path)"
      create => "false",
      perms => mog("600", "$(user)", "$(group)"),
      comment => "Set permission of postfix keytab",
      classes => if_repaired("keytab_wrong_perms");

      # Set configuation variables
      "$(login_conf_path)"
      create => "true",
      perms => mog("644", "$(user)", "$(group)"),
      edit_template => "$(sys.workdir)/inputs/promises/tomcat-login.conf.template",
      comment => "Set JAAS login.conf variables",
      classes => if_repaired("set_login_conf_vars");

      # Set configuation variables
      "$(server_xml_path)"
      create => "true",
      perms => mog("644", "$(user)", "$(group)"),
      edit_template => "$(sys.workdir)/inputs/promises/tomcat-server.xml.template",
      comment => "Set tomcat server configuration",
      classes => if_repaired("edit_server_xml");

      # Set configuation variables
      "$(context_xml_path)"
      create => "true",
      perms => mog("644", "$(user)", "$(group)"),
      edit_template => "$(sys.workdir)/inputs/promises/tomcat-context.xml.template",
      comment => "Set tomcat context configuration",
      classes => if_repaired("edit_context_xml");

    freebsd::
      # Make sure tomcat is enabled
      "/etc/rc.conf"
      create => "true",
      perms => mog("644", "root", "wheel"),
      edit_line => set_variable_values("tomcat.rc_conf"),
      comment => "Enable tomcat in rc.conf",
      classes => if_repaired("enable_rc_conf");

      # Set java arguments to tomcat
      "/etc/rc.conf"
      create => "true",
      perms => mog("644", "root", "wheel"),
      edit_line => append_if_no_line("$(tomcat7_java_opts)"),
      comment => "Enable tomcat in rc.conf",
      classes => if_repaired("enable_rc_conf");

  reports:
    ldap_key::
      "Copied tomcat-to-ldap SSL key";

    ldap_cert::
      "Copied tomcat-to-ldap SSL certificate";

    copy_keystore::
      "Copied tomcat keystore";

    keytab_wrong_perms::
      "Set permissions on $(keytab_path).  WARNING: Keytab had wrong permissions; possible data breach!";

    set_login_conf_vars::
      "Set JAAS configuration variables in $(login_conf_path)";

    edit_server_xml::
      "Edited $(server_xml_path)";

    edit_context_xml::
      "Edited $(context_xml_path)";

    enable_rc_conf::
      "Enabled tomcat in rc.conf";

    enable_rc_conf::
      "Enabled tomcat in rc.conf";

}
